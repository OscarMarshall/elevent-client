(ns views.event-checkin
  (:require
    [cljs-time.coerce :refer [from-string]]
    [cljs-time.core :refer [after? at-midnight now]]
    [cljs-time.format :refer [formatters unparse]]
    [ajax.core :refer [PUT DELETE]]
    [datascript :as d]
    [secretary.core :as secretary :refer-macros [defroute]]

    [app]
    [util :refer [date-formatter]]
    [resources]))

(defc event-id nil)
(defc= event (when event-id (d/entity resources/events-db event-id)))

(defc attendee-id nil)
(defc= attendee (when attendee-id
                  (first (map (fn [[user-id attendee-id]]
                                (merge (into {} (d/entity resources/users-db user-id))
                                       (into {} (d/entity resources/attendees-db attendee-id))))
                              (d/q '[:find ?e ?a
                                     :in $ ?attendee-id
                                     :where
                                     [?a :AttendeeId ?attendee-id]
                                     [?a :UserId ?e]]
                                   resources/attendees-db
                                   attendee-id)))))

(defc button-loading? false)
(defc= button-text (if button-loading?
                     (list (i :class "spinner loading icon"))
                     (if (:CheckinTime attendee)
                       "Check out"
                       "Check in")))

(defn check-in [attendee-id]
  (reset! button-loading? true)
  (PUT (str app/api-base-url "/attendees/" attendee-id "/checkin")
       {:format :json
        :keywords? true
        :headers
        (if @app/token
          {:Authentication
           (str "Bearer " @app/token)}
          {})
        :handler (fn []
                   (js/console.log "Checked in!")
                   (resources/attendees-endpoint :read nil #(reset! button-loading? false)))}))

(defn check-out [attendee-id]
  (reset! button-loading? true)
  (DELETE (str app/api-base-url "/attendees/" attendee-id "/checkin")
               {:format :json
                :keywords? true
                :headers
                (if @app/token
                  {:Authentication
                   (str "Bearer " @app/token)}
                  {})
                :handler (fn []
                           (js/console.log "Checked out!")
                           (resources/attendees-endpoint :read nil #(reset! button-loading? false)))}))

(def breadcrumbs (list ["Events" "#/events"]
                       [(cell= (:Name event)) (cell= (str "#/events/" event-id))]
                       "Attendees"
                       (cell= (str (:FirstName attendee) " " (:LastName attendee)))
                       "Check-in"))

(defn template []
  (div :class "ui page grid"
    (div :class "sixteen wide column"
      (div :class "ui segment"
        (div
          (div :class "ui vertical segment"
            (h1 :class "ui header"
              (text "~{(:FirstName attendee)} ~{(:LastName attendee)}")))
          (div :class "ui vertical segment"
            (div :class "ui divided items"
              (loop-tpl :bindings [event
                                   (cell=
                                     (map #(d/entity resources/events-db
                                                     (first %))
                                          (d/q '[:find ?event-id
                                                 :in $events $attendees ?user-id
                                                 :where
                                                 [$events ?event-id]
                                                 [$attendees ?attendee-id :EventId ?event-id]
                                                 [$attendees ?attendee-id :UserId ?user-id]]
                                               resources/events-db
                                               resources/attendees-db
                                               (:UserId session/user))))]
                (div :class "item"
                  (div :class "content"
                    (a :class "header" :href (str "#/events/" (:EventId @event))
                      (text "~(:Name event)"))
                    (div :class "meta"
                      (b "Date: ")
                      (text
                        (let [start (from-string (:StartDate event))
                              end   (from-string (:EndDate   event))]
                          (str (unparse date-formatter start)
                               (when (after? end start)
                                 (str " to " (unparse date-formatter end)))))))
                    (div :class "description"
                      (text "~{(:Description event)}"))
                    (div :class "extra"
                      (cell=
                        (div :class "ui right floated button"
                             :on-click #(if (:CheckinTime attendee)
                                          (check-out attendee-id)
                                          (check-in attendee-id))
                            button-text))))))))
          (div :class "ui vertical segment"
            (h2 :class "ui header"
              "Attendee Info")
            (table :class "ui definition table attendee-info"
              (cell=
                (tbody
                  (tr
                    (td "Email")
                    (td (:Email attendee)))
                  (tr
                    (td "Gender")
                    (td (:Gender attendee)))
                  (tr
                    (td "High School")
                    (td (:HighSchool attendee)))
                  (tr
                    (td "Class Year")
                    (td (:ClassYear attendee)))
                  (tr
                    (td "Group")
                    (td (:Group attendee))))))))
        (div :do-class (cell= {:ui true
                               :active (empty? resources/events)
                               :dimmer true})
          (div :class "ui loader"))))))

(defroute checkin "/events/:EventId/attendees/:AttendeeId/checkin" [EventId AttendeeId]
  (if @app/token
    (do
      (reset! app/breadcrumbs breadcrumbs)
      (reset! event-id (int EventId))
      (reset! attendee-id (int AttendeeId))
      (template))
    (set! js/location "#/")))
