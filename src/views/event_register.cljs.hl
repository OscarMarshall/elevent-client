(ns views.event-register
  (:require
    [clojure.string :refer [split-lines]]
    [datascript :as d]
    [secretary.core :as secretary :refer-macros [defroute]]
    [cljs-time.coerce :refer [from-string]]
    [cljs-time.core :refer [after? at-midnight]]
    [cljs-time.format :refer [unparse]]

    [app]
    [elements :refer [input-cell]]
    [util :refer [datetime-formatter]]
    [resources]
    [session]))

(defc event-id nil)
(defc= event (when event-id
               (let [entity (d/entity resources/events-db event-id)]
                 (when (seq entity)
                   entity))))

(defc= email (:Email session/user))
(defc= first-name (:FirstName session/user))
(defc= last-name (:LastName session/user))

(defc= form-data {:email email
                  :first-name first-name
                  :last-name last-name})

(defn form-reset! []
  (reset! email nil)
  (reset! first-name nil)
  (reset! last-name nil))

(defn register-event [form-data]
  (resources/attendees-endpoint :create
                                {:UserId (:UserId @session/user)
                                 :EventId @event-id}
                                #(js/location.replace (str "#/events/" @event-id "/schedule"))))

(defn template []
  (div :class "ui stackable page grid"
    (div :class "twelve wide column"
      (div :class "ui segment"
        (div :class "ui vertical segment"
          (h2 :class "ui header"
            (text "Register for ~{(:Name event)}"))
          (div :class "meta"
            (b "Date: ")
            (text
              (when (:StartDate event)
                (let [start (from-string (:StartDate event))
                      end   (from-string (:EndDate   event))]
                  (str (unparse datetime-formatter start)
                       (when (after? end start)
                         (str " to " (unparse datetime-formatter end))))))))
          (div :class "meta"
               (b "Venue: ")
               (text "~{(:Venue event)}"))
          (loop-tpl :bindings [line (cell= (split-lines (:Description event)))]
            (p line)))
        (div :class "ui vertical segment"
          (div :class "ui form"
            (div :class "one field"
              (div :class "required field"
                (label "Email")
                (input-cell :type "text" :state email :disabled "true")))
            (div :class "two fields"
              (div :class "required field"
                (label "First Name")
                (input-cell :type "text" :state first-name :disabled "true"))
              (div :class "required field"
                (label "Last Name")
                (input-cell :type "text" :state last-name :disabled "true")))
            (div :class "ui primary button"
                 :on-click #(register-event @form-data)
              (text "Register"))))))))

(defroute "/events/:EventId/register" [EventId]
  (if @app/token
    (do
      (reset! event-id (int EventId))
      (reset! app/breadcrumbs
              (list ["Events" "#/events"]
                    [(cell= (:Name event)) (str "#/events/" EventId)]
                    "Register"))
      (template))
    (set! js/location "#/")))
