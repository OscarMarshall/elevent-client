(ns views.event-add
  (:require
    [cljs-time.coerce :refer [from-string]]
    [cljs-time.core :refer  [now]]
    [cljs-time.format :refer [formatters unparse]]
    [datascript :as d]
    [secretary.core :as secretary :refer-macros [defroute]]

    [app]
    [elements :refer [input-cell input-field submit]]
    [validateur.validation :as val]
    [resources]))

(defc event-title nil)
(defc organization nil)
(defc venue nil)
(defc start-date (unparse (:date-hour-minute formatters) (now)))
(defc end-date   (unparse (:date-hour-minute formatters) (now)))
(defc description nil)
(defc= form-data
  {:Name event-title
   :StartDate   start-date
   :EndDate     end-date
   :Venue venue
   :Description description
   :Organization organization})
(defc clone-id nil)
(defc= clone-event
  (when clone-id
    (let [entity (d/entity resources/events-db (int clone-id))]
      (when (seq entity)
        entity))))

(defn fill-form [field-name field]
  (->
    (str "#" field-name)
    js/jQuery
    (.val @field)))

(defn populate-form-from-clone! [clone-id elt]
  (prn (int clone-id))
  (prn @clone-event)
  (reset! event-title (:Name @clone-event))
  (prn @event-title)
  (reset! venue (:Venue @clone-event))
  (reset! start-date (:StartDate @clone-event))
  (reset! end-date (:EndDate @clone-event))
  (reset! description (:Description @clone-event))
  (reset! organization (:OrganizationId @clone-event))
  
  ; Populate form fields. I know this is terrible, but it works
  (fill-form "event-title" event-title)
  (fill-form "venue" venue)
  (fill-form "organization" organization)
  (fill-form "start-date" start-date)
  (fill-form "end-date" end-date)
  (fill-form "description" description))

(def validator (val/validation-set
                 (val/presence-of :Name :message "Required field")
                 (val/presence-of :StartDate :message "Required field")
                 (val/presence-of :EndDate :message "Required field")
                 (val/presence-of :Venue :message "Required field")))

(defn create-event [form-data]
  #_(js/console.log (prn-str (@app/validator form-data)))
  (resources/events-endpoint :create
                             (let [start-date (:StartDate form-data)
                                   end-date   (:EndDate form-data)]
                               (assoc form-data
                                 :StartDate (unparse (:date-hour-minute formatters)
                                                     (from-string start-date))
                                 :EndDate (unparse (:date-hour-minute formatters)
                                                   (from-string end-date))))
                             #(set! js/location "#/explore")))

; TODO: eventually filter based on permissions
(defc= events
  (d/q '[:find ?event-id
         :in $ ?user-id
         :where
         [?event-id]]
       resources/events-db
       (:UserId session/user)))

(def breadcrumbs (list ["Events" "#/events"]
                       "Add"))

(defn template []
  (div :class "ui stackable page grid"
    (div :class "sixteen wide column"
      (div :class "ui segment"
        (div :class "ui form"
          (div :class "ui vertical segment"
            (h2 :class "ui dividing header"
              "Add an Event")
            (div :class "two fields"
              (input-field :class "required field" :field :Name :form-data form-data
                (label "Title")
                (input-cell :type "text" :id "event-title" :state event-title))
              (div :class "field"
                (label "Clone From")
                (input-cell :type "select" :class "ui"
                            :state clone-id
                            :on-change #(populate-form-from-clone! @clone-id %)
                            :options (cell= (cons ["None" "val"]
                                                  (map (fn [[event-id]]
                                                         (let [event (when event-id
                                                                       (d/entity resources/events-db event-id))]
                                                           [(:Name event) (:EventId event)]))
                                                       events)))))))
          (div :class "ui vertical segment"
            (div :class "two fields"
              (div :class "required field"
                (label "Organization")
                (input-cell :type "select" :class "ui" :id "organization"
                            :state organization
                            :options (cell=
                                       (map #(let [organization
                                                   (d/entity
                                                     resources/organizations-db
                                                     %)]
                                               [(:Name organization)
                                                (:OrganizationId organization)])
                                        (d/q '[:find [?organization-id ...]
                                              :where [?organization-id]]
                                            resources/organizations-db)))))
              (input-field :class "required field" :field :Venue :form-data form-data
                (label "Venue")
                (input-cell :type "text" :id "venue" :state venue)))
            (div :class "two fields"
              (input-field :class "required field" :field :StartDate :form-data form-data
                (label "Start Date")
                (input-cell :type "datetime-local" :id "start-date" :state start-date))
              (input-field :class "required field" :field :EndDate :form-data form-data
                (label "End Date")
                (input-cell :type "datetime-local" :id "end-date" :state end-date)))
            (div :class "field"
              (label "Description")
              (input-cell :type "textarea" :id "description" :state description))
            (submit :class "ui primary button" :form-data form-data
                    :on-click #(create-event @form-data)
              "Add")))))))

(defroute event-add "/events/add" []
  (if @app/token
    (do
      (reset! app/breadcrumbs breadcrumbs)
      (app/reset-validation! validator)
      (template))
    (set! js/location "#/")))
